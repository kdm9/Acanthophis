#######################################################################
#                                NGMap                                #
#######################################################################
rule ngmap:
    input:
        reads="data/reads/runs/{run}~{lib}.fastq.gz",
        ref=lambda wc: config['refs'][wc.ref]["fasta"],
    output:
        bam=temp("data/alignments/byrun.raw/ngm/{ref}/{run}~{lib}~{sample}.bam"),
    log:
        "data/log/ngm/{ref}~{run}~{lib}~{sample}.log"
    threads:
        8
    resources: **acanthophis.rule_resources(config, "ngmap", time_min=240, mem_gb=16)
    params:
        sensitivity=config["align"]["ngm"]["sensitivity"],
    conda: acanthophis.get_resource("envs/align.yml")
    shell:
        "( ngm"
        "   -q {input.reads}"
        "   --paired --broken-pairs"
        "   -r {input.ref}"
        "   -t {threads}"
        "   --rg-id {wildcards.run}_{wildcards.lib}_{wildcards.sample}"
        "   --rg-sm {wildcards.sample}"
        "   --sensitivity {params.sensitivity}" # this is the mean from a bunch of different runs
        "| samtools view -Suh - >{output.bam}"
        " ) >{log} 2>&1"


#######################################################################
#                               BWAMem                                #
#######################################################################
rule bwaidx:
    input:
        "{path}"
    output:
        "{path}.amb",
        "{path}.ann",
        "{path}.bwt",
        "{path}.pac",
        "{path}.sa",
    threads: 1
    conda: acanthophis.get_resource("envs/align.yml")
    log: "data/log/bwaidx/{path}.log"
    resources: **acanthophis.rule_resources(config, "bwaidx", time_min=20, mem_gb=8)
    shell:
        "bwa index {input} >{log} 2>&1"


rule bwamem:
    input:
        reads="data/reads/runs/{run}~{lib}.fastq.gz",
        ref=lambda wc: config['refs'][wc.ref]["fasta"],
        bwaidx=lambda wc: config['refs'][wc.ref]["fasta"]+ ".pac",
    output:
        bam=temp("data/alignments/byrun.raw/bwa/{ref}/{run}~{lib}~{sample}.bam"),
    log: "data/log/bwa/{ref}~{run}~{lib}~{sample}.log"
    threads:
        8
    resources: **acanthophis.rule_resources(config, "bwamem", time_min=240, mem_gb=16)
    conda: acanthophis.get_resource("envs/align.yml")
    shell:
        "( bwa mem"
        "   -p" # paired input
        "   -t {threads}"
        "   -R '@RG\\tID:{wildcards.run}_{wildcards.lib}_{wildcards.sample}\\tSM:{wildcards.sample}'"
        "   {input.ref}"
        "   {input.reads}"
        "| samtools view -Suh - >{output.bam}"
        " ) >{log} 2>&1"


#######################################################################
#                                ABRA2                                #
#######################################################################
# ABRA2 requires input from bwa mem. We run ABRA2 as just another aligner,
# whose name is bwa_abra2. One can use the outputs from abra2 by subsituting
# `bwa` for `bwa_abra2` in the `aligner` wildcard of any subsequent rule.
rule abra2:
    input:
        bam="data/alignments/samples/bwa/{ref}/{sample}.bam",
        bai="data/alignments/samples/bwa/{ref}/{sample}.bam.bai",
        ref=lambda wc: config['refs'][wc.ref]["fasta"],
    output:
        bam=temp("data/alignments/samples/bwa_abra2/{ref}/{sample}.bam"),
    log: "data/log/abra2/{ref}~{sample}.log"
    threads:
        8
    resources: **acanthophis.rule_resources(config, "abra2", time_min=360, mem_gb=16)
    params:
        java_args=config["align"].get("abra2", {}).get("java_args", "-Xmx4G"),
        extra_args=lambda wc: config["align"].get("abra2", {}).get("extra_args", {}).get(wc.ref, ""),
        temp_dir=config.get("misc", {}).get("tmpdir", "$TMPDIR"),
    conda: acanthophis.get_resource("envs/abra2.yml")
    shell:
        "( export JAVA_TOOL_OPTIONS='{params.java_args}'; "
        "  abra2"
        "   {params.extra_args}"
        "   --threads {threads}"
        "   --in {input.bam}"
        "   --ref {input.ref}"
        "   --out {output.bam}"
        " ) >{log} 2>&1"


#######################################################################
#                     Merge runlib -> sample bams                     #
#######################################################################
rule mergebam_samp:
    input:
        lambda wc: ["data/alignments/byrun.raw/{aln}/{ref}/{run}~{lib}~{sample}.bam".format(
                            run=r, lib=l, aln=wc.aligner, ref=wc.ref, sample=wc.sample)
	                for r, l in config["SAMP2RUNLIB"][wc.sample]]
    output:
        bam=temp("data/alignments/samples.raw/{aligner}/{ref}/{sample}.bam"),
    log:
        "data/log/mergesamplebam/{aligner}~{ref}~{sample}.log"
    threads: 4
    resources: **acanthophis.rule_resources(config, "mergebam_samp", time_min=120, mem_gb=16)
    conda: acanthophis.get_resource("envs/align.yml")
    shell:
        "( samtools merge"
        "   -@ {threads}"
        "   --output-fmt bam,level=0"
        "   {output.bam}"
        "   {input}"
        " ) >{log} 2>&1"


#######################################################################
#                        samtools sort/markdup                        #
#######################################################################
rule bam_markdups_sort:
    input:
        bam="data/alignments/samples.raw/{aligner}/{ref}/{sample}.bam",
        ref=lambda wc: config['refs'][wc.ref]["fasta"],
    output:
        bam="data/alignments/samples/{aligner}/{ref}/{sample}.bam",
    threads: 4
    resources: **acanthophis.rule_resources(config, "bam_markdups_sort", time_min=240, mem_gb=16)
    log: "data/log/markdup/{aligner}~{ref}~{sample}.log"
    conda: acanthophis.get_resource("envs/align.yml")
    priority: 1 # so the temps get cleaned sooner
    shell:
        "( samtools fixmate "
        "   -m"
        "   -@ {threads}"
        "   --output-fmt bam,level=0"
        "   {input.bam}"
        "   -"
        " | samtools sort"
        "   -T ${{TMPDIR:-/tmp}}/{wildcards.sample}_sort_$RANDOM"
        "   --output-fmt bam,level=0"
        "   -@ {threads}"
        "   -m 1g"
        "   -"
        " | samtools markdup"
        "   -T ${{TMPDIR:-/tmp}}/{wildcards.sample}_markdup_$RANDOM"
        "   -s" # report stats
        "   -@ {threads}"
        "   --output-fmt bam,level=8"
        "   -"
        "   {output.bam}"
        " ) >{log} 2>&1"


#######################################################################
#                     Merge samples -> samplesets                     #
#######################################################################

rule mergebam_set:
    input:
        lambda wc: expand("data/alignments/samples/{aligner}/{ref}/{sample}.bam",
                          aligner=wc.aligner, ref=wc.ref, sample=config["SAMPLESETS"][wc.sampleset]),
    output:
        bam="data/alignments/sets/{aligner}~{ref}~{sampleset}.bam",
        bai="data/alignments/sets/{aligner}~{ref}~{sampleset}.bam.bai",
    log:
        "data/log/mergesetbam/{aligner}~{ref}~{sampleset}.log"
    threads: 16
    resources: **acanthophis.rule_resources(config, "mergebam_set", time_min=2880, mem_gb=16)
    conda: acanthophis.get_resource("envs/align.yml")
    shell:
        "( samtools merge"
        "   --output-fmt bam,level=8"
        "   -@ {threads}"
        "   -"
        "   {input}"
        " | tee {output.bam}"
        " | samtools index - {output.bai}"  # indexing takes bloody ages, we may as well do this on the fly
        " ) >{log} 2>&1"


#######################################################################
#                              Bam Index                              #
#######################################################################
# Ruleorder needed here as we index on the fly for sets. prefer the BAI from
# the on-the-fly indexing for merged set bams, 
ruleorder: mergebam_set > bamidx
rule bamidx:
    input:
        "{path}.bam"
    output:
        "{path}.bam.bai"
    log:
        "data/log/bamindex/{path}.log"
    conda: acanthophis.get_resource("envs/align.yml")
    shell:
        "samtools index {input}"


#######################################################################
#                              Bam stats                              #
#######################################################################
rule bamstat_sample:
    input:
        "data/alignments/samples/{aligner}/{ref}/{sample}.bam",
    output:
        "data/alignments/bamstats/sample/{aligner}~{ref}~{sample}.samtools.stats",
    log:
        "data/log/bamstat_sample/{aligner}~{ref}~{sample}.log"
    threads: 1
    resources: **acanthophis.rule_resources(config, "bamstat_sample", time_min=120, mem_gb=4)
    conda: acanthophis.get_resource("envs/align.yml")
    shell:
        "(samtools stats -i 5000 -x {input} >{output}) >{log}"

rule qualimap_sample:
    input:
        bam="data/alignments/samples/{aligner}/{ref}/{sample}.bam",
    output:
        directory("data/alignments/qualimap/samples/{aligner}~{ref}~{sample}/"),
    log:
        "data/log/qualimap_sample/{aligner}~{ref}~{sample}.log"
    resources: **acanthophis.rule_resources(config, "qualimap_sample", time_min=120, mem_gb=8)
    conda: acanthophis.get_resource("envs/qualimap.yml")
    threads: 4
    shell:
        "( unset DISPLAY; qualimap bamqc"
        "   --java-mem-size=7G"
        "   -bam {input.bam}"
        "   -nr 10000"
        "   -nt {threads}"
        "   -outdir {output}"
        "   {input}"
        " ) >{log} 2>&1"


#######################################################################
#                         Align Target Rules                          #
#######################################################################
localrules: align_librun
rule align_librun:
    input:
        lambda wc: ["data/alignments/byrun/{aln}/{ref}/{run}~{lib}.bam".
                        format(run=r, lib=l, aln=a, ref=ref)
                        for r, l in config["RUNLIB2SAMP"]
                        for a in config["align"]["aligners"]
                        for ref in config["align"]["refs"]],


localrules: align_samples
rule align_samples:
    input:
        expand("data/alignments/samples/{aligner}/{ref}/{sample}.bam",
               ref=config["align"]["refs"],
               aligner=config["align"]["aligners"],
               sample=config["SAMP2RUNLIB"]),



localrules: align_samplesets
rule align_samplesets:
    input:
        expand("data/alignments/sets/{aligner}~{ref}~{sampleset}.bam",
               ref=config["align"]["refs"],
               aligner=config["align"]["aligners"],
               sampleset=config["align"]["samplesets"])

localrules: align
rule align:
   input:
        rules.align_samples.input,
        rules.align_samplesets.input,
