# These rules are part of Acanthophis. See https://github.com/kdm9/Acanthophis.
# This file *could* be modified, but then be careful when you update them. And
# please, if you find a bug, raise an issue on github so the fix gets shared
# with everyone.
#
# Copyright 2016-2022 Kevin Murray/Gekkonid Consulting
#
# This Source Code Form is subject to the terms of the Mozilla Public License,
# v. 2.0. If a copy of the MPL was not distributed with this file, You can
# obtain one at http://mozilla.org/MPL/2.0/.

#######################################################################
#                                NGMap                                #
#######################################################################
rule ngmap_idx:
    input:
        ref=R("{path}")
    output:
        R("{path}-enc.2.ngm"),
        R("{path}-ht-13-2.3.ngm"),
    log:
        L("{path}-index.log"),
    resources: **rule_resources(config, "ngmap_idx", runtime=40, mem_gb=2, cores=1)
    conda: "envs/align.yml"
    shell:
        "( ngm"
        "   -r {input.ref}"
        " ) >{log} 2>&1"

rule ngmap:
    input:
        reads=T("reads/runs/{run}~{lib}.fastq.gz"),
        ref=lambda wc: R(config["data_paths"]['references'][wc.ref]["fasta"], keep_local=True),
        idx=lambda wc: R(config["data_paths"]['references'][wc.ref]["fasta"] + "-enc.2.ngm"),
    output:
        bam=temp(T("alignments/byrun.raw/ngm/{ref}/{run}~{lib}~{sample}.bam")),
    log:
        L("alignments/byrun.raw/ngm/{ref}/{run}~{lib}~{sample}.bam.log"),
    resources: **rule_resources(config, "ngmap", runtime=240, mem_gb=16, cores=8)
    params:
        sensitivity=config["tool_settings"]["ngm"]["sensitivity"],
    conda: "envs/align.yml"
    shell:
        "( ngm"
        "   -q {input.reads}"
        "   --paired --broken-pairs"
        "   -r {input.ref}"
        "   -t {threads}"
        "   --rg-id {wildcards.run}_{wildcards.lib}_{wildcards.sample}"
        "   --rg-sm {wildcards.sample}"
        "   --sensitivity {params.sensitivity}"
        "| samtools view -Suh - >{output.bam}"
        " ) >{log} 2>&1"


#######################################################################
#                               BWAMem                                #
#######################################################################
rule bwaidx:
    input:
        R("{path}")
    output:
        R("{path}.amb"),
        R("{path}.ann"),
        R("{path}.bwt"),
        R("{path}.pac"),
        R("{path}.sa"),
    conda: "envs/align.yml"
    log: L("{path}_index.log"),
    resources: **rule_resources(config, "bwaidx", runtime=20, mem_gb=8)
    shell:
        "bwa index {input} >{log} 2>&1"


rule bwamem:
    input:
        reads=T("reads/runs/{run}~{lib}.fastq.gz"),
        ref=lambda wc: R(config["data_paths"]['references'][wc.ref]["fasta"], keep_local=True),
        bwaidxamb=lambda wc: R(config["data_paths"]['references'][wc.ref]["fasta"]+ ".amb", keep_local=True),
        bwaidxann=lambda wc: R(config["data_paths"]['references'][wc.ref]["fasta"]+ ".ann", keep_local=True),
        bwaidxbwt=lambda wc: R(config["data_paths"]['references'][wc.ref]["fasta"]+ ".bwt", keep_local=True),
        bwaidxpac=lambda wc: R(config["data_paths"]['references'][wc.ref]["fasta"]+ ".pac", keep_local=True),
        bwaidxsa=lambda wc: R(config["data_paths"]['references'][wc.ref]["fasta"]+ ".sa", keep_local=True),
    output:
        bam=temp(T("alignments/byrun.raw/bwa/{ref}/{run}~{lib}~{sample}.bam")),
    log: L("alignments/byrun.raw/bwa/{ref}/{run}~{lib}~{sample}.bam.log")
    resources: **rule_resources(config, "bwamem", runtime=240, mem_gb=10, cores=8)
    conda: "envs/align.yml"
    shell:
        "( bwa mem"
        "   -p" # paired input
        "   -t {threads}"
        "   -R '@RG\\tID:{wildcards.run}_{wildcards.lib}_{wildcards.sample}\\tSM:{wildcards.sample}'"
        "   {input.ref}"
        "   {input.reads}"
        " | samtools view -Suh - >{output.bam}"
        " ) >{log} 2>&1"


#######################################################################
#                     samtools merge/sort/markdup                     #
#######################################################################
rule bam_merge_markdups_sort:
    input:
        bams=lambda wc: T(["alignments/byrun.raw/{aln}/{ref}/{run}~{lib}~{sample}.bam".format(
                            run=r, lib=l, aln=wc.aligner, ref=wc.ref, sample=wc.sample)
	                for r, l in config["SAMP2RUNLIB"][wc.sample]]),
        ref=lambda wc: R(config["data_paths"]['references'][wc.ref]["fasta"], keep_local=True),
    output:
        bam=T("alignments/samples/{aligner}~{ref}~{sample}.bam"),
        bai=T("alignments/samples/{aligner}~{ref}~{sample}.bam.bai"),
    resources: **rule_resources(config, "bam_merge_markdups_sort", runtime=240, mem_gb=16, disk_gb=50, cores=8)
    log: L("alignments/samples/{aligner}~{ref}~{sample}.bam.log")
    conda: "envs/align.yml"
    priority: 2
    params:
        ziplevel=config.get("tool_settings", {}).get('ziplevel', 6),
        sortmem=config.get("tool_settings", {}).get("samtools", {}).get("sortmem_mb", 100),
    shell:
        "( samtools merge"
        "   -@ {threads}"
        "   -u"
        "   -o /dev/stdout"
        "   {input.bams}"
        " | samtools fixmate "
        "   -m"
        "   -@ {threads}"
        "   -u"
        "   /dev/stdin"
        "   /dev/stdout"
        " | samtools sort"
        "   -T ${{TMPDIR:-/tmp}}/{wildcards.sample}_sort_$RANDOM"
        "   --output-fmt bam,level=0"
        "   -@ {threads}"
        "   -m {params.sortmem}m" # multiplied by {threads}
        "   /dev/stdin"
        " | samtools markdup"
        "   -T ${{TMPDIR:-/tmp}}/{wildcards.sample}_markdup_$RANDOM"
        "   -s" # report stats
        "   -@ {threads}"
        "   --output-fmt bam,level={params.ziplevel}"
        "   /dev/stdin"
        "   /dev/stdout"
        " | tee {output.bam}"
        " | samtools index - {output.bai}"  # indexing takes bloody ages, we may as well do this on the fly
        " ) > {log} 2>&1"

localrules: sample_bam_persist
rule sample_bam_persist:
    input:
        bam=T("alignments/samples/{aligner}~{ref}~{sample}.bam"),
        bai=T("alignments/samples/{aligner}~{ref}~{sample}.bam.bai"),
    output:
        bam=P("alignments/samples/{aligner}~{ref}~{sample}.bam"),
        bai=P("alignments/samples/{aligner}~{ref}~{sample}.bam.bai"),


#######################################################################
#                     Merge samples -> samplesets                     #
#######################################################################

rule mergebam_set:
    input:
        lambda wc: T(expand("alignments/samples/{aligner}~{ref}~{sample}.bam",
                          aligner=wc.aligner, ref=wc.ref, sample=config["SAMPLESETS"][wc.sampleset])),
    output:
        bam=T("alignments/sets/{aligner}~{ref}~{sampleset}.bam"),
        bai=T("alignments/sets/{aligner}~{ref}~{sampleset}.bam.bai"),
    log:
        L("alignments/sets/{aligner}~{ref}~{sampleset}.bam.log"),
    resources: **rule_resources(config, "mergebam_set", runtime=2880, mem_gb=16, disk_gb=1000, cores=64)
    conda: "envs/align.yml"
    params:
        ziplevel=config.get("tool_settings", {}).get('ziplevel', 6),
    shell:
        "( samtools merge"
        "   --output-fmt bam,level={params.ziplevel}"
        "   -@ {threads}"
        "   -"
        "   {input}"
        " | tee {output.bam}"
        " | samtools index - {output.bai}"  # indexing takes bloody ages, we may as well do this on the fly
        " ) >{log} 2>&1"


#######################################################################
#                              Bam stats                              #
#######################################################################
rule bamstat_sample:
    input:
        T("alignments/samples/{aligner}~{ref}~{sample}.bam"),
    output:
        P("alignments/bamstats/sample/{aligner}~{ref}~{sample}.samtools.stats"),
    log:
        L("alignments/bamstats/sample/{aligner}~{ref}~{sample}.samtools.stats.log")
    resources: **rule_resources(config, "bamstat_sample", runtime=120, mem_gb=4, cores=1)
    conda: "envs/align.yml"
    shell:
        "(samtools stats -i 5000 -x {input} >{output}) >{log} 2>&1"


rule multiqc_samstats:
    input:
        stats=lambda wc: P(expand("alignments/bamstats/sample/{{aligner}}~{{ref}}~{sample}.samtools.stats", sample=config["SAMPLESETS"][wc.sampleset])),
    output:
        html=P("stats/multiqc/bamstats_{aligner}~{ref}~{sampleset}_multiqc.html"),
    log:
        log=L("stats/multiqc/bamstats_{aligner}~{ref}~{sampleset}_multiqc.log"),
    resources: **rule_resources(config, "multiqc_samstats", runtime=30, mem_gb=2, cores=1)
    conda: "envs/qcstats.yml"
    shell:
        "multiqc"
        "   --no-megaqc-upload"
        "   --interactive"
        "   --no-data-dir"
        "   --comment 'Samtools stats for sample set {wildcards.sampleset} against {wildcards.ref} with {wildcards.aligner}'"
        "   --filename {output.html}"
        "   {input.stats}"
        " >{log} 2>&1"

#######################################################################
#                              Qualimap                               #
#######################################################################

rule qualimap_sample:
    input:
        bam=T("alignments/samples/{aligner}~{ref}~{sample}.bam"),
        bai=T("alignments/samples/{aligner}~{ref}~{sample}.bam.bai"),
    output:
        bamqc=directory(P("alignments/qualimap/samples/{aligner}~{ref}~{sample}/")),
    log:
        L("log/qualimap_sample/{aligner}~{ref}~{sample}.log")
    resources: **rule_resources(config, "qualimap_sample", runtime=240, mem_gb=8, cores=8)
    conda: "envs/qualimap.yml"
    shell:
        "( unset DISPLAY; qualimap bamqc"
        "   --java-mem-size=7G"
        "   -bam {input.bam}"
        "   -nt {threads}"
        "   -nr 10000"
        "   -outdir {output}"
        "   {input.bam}"
        " ) >{log} 2>&1"

rule multiqc_qualimap:
    input:
        qm=lambda wc: P(expand("alignments/qualimap/samples/{{aligner}}~{{ref}}~{sample}/", sample=config["SAMPLESETS"][wc.sampleset])),
    output:
        html=P("stats/multiqc/qualimap_{aligner}~{ref}~{sampleset}_multiqc.html"),
    log:
        log=L("stats/multiqc/qualimap_{aligner}~{ref}~{sampleset}_multiqc.log"),
    resources: **rule_resources(config, "multiqc_qualimap", runtime=30, mem_gb=2, cores=1)
    conda: "envs/qcstats.yml"
    shell:
        "multiqc"
        "   --no-megaqc-upload"
        "   --interactive"
        "   --no-data-dir"
        "   --comment 'Qualimap summary for sample set {wildcards.sampleset} against {wildcards.ref} with {wildcards.aligner}'"
        "   --filename {output.html}"
        "   {input.qm}"
        " >{log} 2>&1"

#######################################################################
#                           Unmapped reads                            #
#######################################################################

rule extract_unmapped:
    input:
        T("alignments/samples/{aligner}~{ref}~{sample}.bam"),
    output:
        r1=T("alignments/unmapped_reads/{aligner}~{ref}~{sample}_R1.fastq.gz"),
        r2=T("alignments/unmapped_reads/{aligner}~{ref}~{sample}_R2.fastq.gz"),
        se=T("alignments/unmapped_reads/{aligner}~{ref}~{sample}_se.fastq.gz"),
    log:
        L("alignments/unmapped_reads/{aligner}~{ref}~{sample}.fastq.gz.log"),
    resources: **rule_resources(config, "extract_unmapped", runtime=120, mem_gb=1, cores=8)
    conda: "envs/align.yml"
    params:
        ziplevel=config.get("tool_settings", {}).get('ziplevel', 6),
    shell:
        "(samtools fastq "
        "   -t"
        "   -f4"
        "   -N"
        "   -c {params.ziplevel}"
        "   -@ {threads}"
        "   -1 {output.r1}"
        "   -2 {output.r2}"
        "   -s {output.se}"
        "   -0 {output.se}"  # 0 is "other", i.e. singles
        "   {input}"
        ") >{log} 2>&1"



#######################################################################
#                         Align Target Rules                          #
#######################################################################


rule all_align_samples:
    input:
        [ P(expand("alignments/samples/{aligner}~{ref}~{sample}.bam",
                 ref=config["samplesets"][ss].get("align", {}).get("references", []),
                 aligner=config["samplesets"][ss].get("align", {}).get("aligners", []),
                 sample=config["SAMPLESETS"][ss]))
          for ss in config["samplesets"]
          if config["samplesets"][ss].get("align", {}).get("keep_bams", False) ],
        [ P(expand("stats/multiqc/bamstats_{aligner}~{ref}~{sampleset}_multiqc.html",
                   ref=config["samplesets"][ss].get("align", {}).get("references", []),
                   aligner=config["samplesets"][ss].get("align", {}).get("aligners", []),
                   sampleset=ss))
          for ss in config["samplesets"]
          if config["samplesets"][ss].get("align", {}).get("stats", False) ],
        [ P(expand("stats/multiqc/qualimap_{aligner}~{ref}~{sampleset}_multiqc.html",
                   ref=config["samplesets"][ss].get("align", {}).get("references", []),
                   aligner=config["samplesets"][ss].get("align", {}).get("aligners", []),
                   sampleset=ss))
          for ss in config["samplesets"]
          if config["samplesets"][ss].get("align", {}).get("qualimap", False) ],


rule all_unmapped_reads:
    input:
        [ T(expand("alignments/unmapped_reads/{aligner}~{ref}~{sample}_{r}.fastq.gz",
                 r=["R1", "R2", "se"],
                 ref=config["samplesets"][ss].get("align", {}).get("references", []),
                 aligner=config["samplesets"][ss].get("align", {}).get("aligners", []),
                 sample=config["SAMPLESETS"][ss]))
          for ss in config["samplesets"]
          if config["samplesets"][ss].get("align", {}).get("unmapped_reads", False) ]

rule all_align_samplesets:
    input:
        [ T(expand("alignments/sets/{aligner}~{ref}~{sampleset}.bam",
                 ref=config["samplesets"][ss].get("align", {}).get("references", []),
                 aligner=config["samplesets"][ss].get("align", {}).get("aligners", []),
                 sampleset=ss))
          for ss in config["samplesets"]
          if config["samplesets"][ss].get("align", {}).get("sampleset_bams", False) ]

rule all_align:
    input:
        rules.all_align_samples.input,
        rules.all_unmapped_reads.input,
        rules.all_align_samplesets.input,
