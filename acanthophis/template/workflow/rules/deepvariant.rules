# These rules are part of Acanthophis. See https://github.com/kdm9/Acanthophis.
# This file *could* be modified, but then be careful when you update them. And
# please, if you find a bug, raise an issue on github so the fix gets shared
# with everyone.
#
# Copyright 2020-2024 Kevin Murray/Gekkonid Consulting
#
# This Source Code Form is subject to the terms of the Mozilla Public License,
# v. 2.0. If a copy of the MPL was not distributed with this file, You can
# obtain one at http://mozilla.org/MPL/2.0/.

rule deepvariant_gvcf:
    input:
        bam=T("alignments/samples/{aligner}~{ref}~{sample}.bam"),
        bai=T("alignments/samples/{aligner}~{ref}~{sample}.bam.bai"),
        ref=lambda wc: R(config["data_paths"]["references"][wc.ref]["fasta"], keep_local=True),
    output:
        vcf=T("deepvariant/{aligner}~{ref}/{sample}.vcf.gz"),
        gvcf=T("deepvariant/{aligner}~{ref}/{sample}.g.vcf.gz"),
    log:
        T("deepvariant/{aligner}~{ref}/{sample}_examples.log"),
    #conda:
    #    "envs/deepvariant.yml",
    container:
        "docker://google/deepvariant:1.6.0"
    params:
        model=lambda wc: config["tool_settings"]["varcall"].get("deepvariant_model", "WGS"),
        extra="",
    shadow: "shallow"
    resources: **rule_resources(config, "deepvariant_gvcf", runtime=600, mem_gb=96, cores=32, disk_mb=400_000)
    shell:
        "( /opt/deepvariant/bin/run_deepvariant"
        "   --model_type={params.model}"
        "   --ref={input.ref}"
        "   --make_examples_extra_args 'normalize_reads=true'"
        "   --reads={input.bam}"
        "   --output_vcf={output.vcf}"
        "   --output_gvcf={output.gvcf}"
        "   --intermediate_results_dir=$TMPDIR"
        "   --num_shards={threads}"
        ") &> {log}"


localrules: glnexus_fofn
rule glnexus_fofn:
    input:
        gvcf=lambda wc: T(expand("deepvariant/{aligner}~{ref}/{sample}.g.vcf.gz",
                                 aligner=wc.aligner, ref=wc.ref,
                                 sample=config["SAMPLESETS"][wc.sampleset])),
    output:
        T("deepvariant/{aligner}~{ref}~{sampleset}.gvcf_fofn.txt"),
    run:
        with open(output[0], "w") as fh:
            for gvcf in input:
                print(gvcf, file=fh)

rule glnexus_call:
    input:
        gvcf=lambda wc: T(expand("deepvariant/{aligner}~{ref}/{sample}.g.vcf.gz",
                                 aligner=wc.aligner, ref=wc.ref,
                                 sample=config["SAMPLESETS"][wc.sampleset])),
        fofn=T("deepvariant/{aligner}~{ref}~{sampleset}.gvcf_fofn.txt"),
    output:
        vcf=T("deepvariant/{aligner}~{ref}~{sampleset}.vcf.gz"),
    log:
        T("deepvariant/{aligner}~{ref}~{sampleset}.vcf.gz.log"),
    conda:
        "envs/glnexus.yml",
    container:
        "docker://ghcr.io/kdm9/glnexus-bcftools:latest"
    shadow: "shallow"
    resources: **rule_resources(config, "glnexus_call", runtime=180, mem_gb=512, cores=128)
    shell:
        "( glnexus_cli"
        "   --threads {threads}"
        "   --mem-gb 500"
        "   --dir $TMPDIR/{wildcards.aligner}~{wildcards.ref}~{wildcards.sampleset}"
        "   --config DeepVariant"
        "   --list"
        "   {input.fofn}"
        " | bcftools view -Oz8 --threads {threads} -o {output.vcf}"
        ") &> {log}"



#######################################################################
#                             Target Rules                            #
#######################################################################
rule all_deepvariant:
    input:
        [T(expand("deepvariant/{aligner}~{ref}~{sampleset}.vcf.gz",
               aligner=config["samplesets"][sampleset]["varcall"]["aligners"],
               ref=config["samplesets"][sampleset]["varcall"]["refs"],
               sampleset=sampleset))
         for sampleset in config["samplesets"]
         if "deepvariant" in config["samplesets"][sampleset].get("varcall", {}).get("callers", [])
         ],
